use std::str::FromStr;
use crate::tokens::*;
use crate::codegen::{ComparisonOp, QLType};

grammar;

pub Program: ProgramNode =
    <datasources:Datasource*>
    <tables:Table*>
    <functions:Function*> => ProgramNode { <> };

Datasource: DatasourceNode = "datasource" <name:QName> ";" => DatasourceNode { <> };

Table: TableNode =
    "table" <name:ProperQName> "from" <datasource_name:QName>
    "{" <columns:Comma<TypedQName>> "}" => TableNode { <> };

Function: FunctionNode =
    "function" <name:QName>
    "(" <params:Comma<TypedQName>> ")"
    "->" <return_type:TypeNameWithVoid>
    "{" <body:Statement*> "}"
    => FunctionNode { <> };

Statement: StatementNode = {
    <TypedQName> "<-" <Expression> ";" => StatementNode::VariableDefinition(<>),
    <QName> "<-" <Expression> ";" => StatementNode::Assignment(<>),
    <ConditionalStatement>,
    "while" <Expression> "{" <Statement*> "}" <("as" <QName> ";")?> => StatementNode::ConditionalLoop(<>),
    "return" <Expression?> ";" => StatementNode::Return(<>),
    "break" <QName?> ";" => StatementNode::Break(<>),
    "continue" <QName?> ";" => StatementNode::Continue(<>),
    <Expression> ";" => StatementNode::LoneExpression(<>)
}

ConditionalStatement: StatementNode =
    "if" <head:ConditionalBranch>
    <middle:("else" "if" <ConditionalBranch>)*>
    <else_body:("else" "{" <Statement*> "}")?>
=> {
    let mut branches = vec![head];
    branches.extend(middle.into_iter());
    StatementNode::Conditional(branches, else_body)
};

ConditionalBranch: ConditionalBranchNode =
    <condition:Expression> "{" <body:Statement*> "}" => ConditionalBranchNode { <> };

Expression: Box<ExpressionNode> = {
    #[precedence(level="0")]
    Int => Box::new(ExpressionNode::IntegerLiteral(<>)),
    Bool => Box::new(ExpressionNode::BoolLiteral(<>)),
    QLString => Box::new(ExpressionNode::StringLiteral(<>)),
    <name:ProperQName> "{" <cols:Comma<ColumnValue>> "}" => Box::new(ExpressionNode::TableRow(<>)),
    <TypeName> "[" <Comma<Expression>> "]" => Box::new(ExpressionNode::Array(<>)),
    <Expression> "[" <Expression> "]" => Box::new(ExpressionNode::ArrayIndex(<>)),
    <Expression> "." <QName> "(" <Comma<Expression>> ")" => Box::new(ExpressionNode::MethodCall(<>)),
    <Expression> "." <QName> => Box::new(ExpressionNode::Column(<>)),
    QName => Box::new(ExpressionNode::QName(<>)),
    #[precedence(level="1")] #[assoc(side="left")]
    <Expression> "+" <Expression> => Box::new(ExpressionNode::Add(<>)),
    <Expression> "-" <Expression> => Box::new(ExpressionNode::Subtract(<>)),
    <QName> "(" <Comma<Expression>> ")" => Box::new(ExpressionNode::FunctionCall(<>)),
    Query => Box::new(ExpressionNode::Query(<>)),
    #[precedence(level="2")] #[assoc(side="left")]
    <Expression> ">" <Expression> => Box::new(ExpressionNode::Comparison(<>, ComparisonOp::GreaterThan)),
    <Expression> "<" <Expression> => Box::new(ExpressionNode::Comparison(<>, ComparisonOp::LessThan)),
    <Expression> ">=" <Expression> => Box::new(ExpressionNode::Comparison(<>, ComparisonOp::GreaterThanOrEqual)),
    <Expression> "<=" <Expression> => Box::new(ExpressionNode::Comparison(<>, ComparisonOp::LessThanOrEqual)),
    #[precedence(level="3")] #[assoc(side="left")]
    <Expression> "=" <Expression> => Box::new(ExpressionNode::Comparison(<>, ComparisonOp::Equal)),
    <Expression> "!=" <Expression> => Box::new(ExpressionNode::Comparison(<>, ComparisonOp::NotEqual))
}

ColumnValue: ColumnValueNode = <name:QName> ":" <value:Expression> => ColumnValueNode { <> };

Query: QueryNode = "query" "{"
    "select" "from" <table_name:ProperQName>
    <where_clause:WhereClause?>
"}" => QueryNode { <> };

WhereClause: WhereNode = "where" <column_name:QName> "=" <value:Expression> => WhereNode { <> };

TypedQName: TypedQNameNode = <ql_type:TypeName> <name:QName> => TypedQNameNode { <> };

TypeNameWithVoid: QLType = {
    <TypeName>,
    "void" => QLType::Void
}

TypeName: QLType = {
    "int" => QLType::Integer,
    "bool" => QLType::Bool,
    "str" => QLType::String,
    ProperQName => QLType::Table(<>),
    <TypeName> "[]" => QLType::Array(Box::new(<>))
}

Int: i32 = r"-?[0-9]+" => i32::from_str(<>).unwrap();
Bool: bool = { "true" => true, "false" => false };
QLString: String = <s:r#""[^"]*""#> => s[1..s.len()-1].to_string();

ProperQName: String = r"_*[A-Z][a-zA-Z0-9]*" => String::from(<>);
QName: String = r"_*[a-z][a-zA-Z0-9_]*" => String::from(<>);

Comma<T>: Vec<T> = _Comma<T>? => match <> {
    Some(xs) => xs,
    None => vec![]
};

_Comma<T>: Vec<T> = <head:T> <tail:("," <T>)*> => {
    let mut vals = vec![head];
    vals.extend(tail.into_iter());
    vals
};
